// do a valid LOCK + LOCKED + invalid UNLOCKING

run()
listen(address: <127.0.0.1:20002>)

label(name: wait_message)
wait(timeout: 12, mode: wait)

label(name: process_message)
has_message()
if(false: wait_message)

show_message()

has_message(command: LOCK)
if(false: not_lock)
verify_message(
	command: LOCK,
	service: cluckd,
	required_parameters: {
		object_name: "unlocking-lock",
		tag: `^[0-9]+$`,
		pid: `^[0-9]+$`,
		serial: `^[0-9]+$`,
		timeout: `^[0-9]+\\.[0-9]+$`
	})
save_parameter_value(parameter_name: tag, variable_name: tag)
save_parameter_value(parameter_name: pid, variable_name: pid)
save_parameter_value(parameter_name: serial, variable_name: serial)
now(variable_name: now)
set_variable(name: locked_date, value: ${now} + 1)
set_variable(name: unlocked_date, value: ${locked_date} + 5)
send_message(
	command: LOCKED,
	sent_server: my_server,
	sent_service: cluckd,
	server: lock_server,
	service: cluck_test,
	parameters: {
		object_name: "unlocking-lock",
		tag: "${tag}",
		timeout_date: ${locked_date},
		unlocked_date: ${unlocked_date}
	})
clear_message()

// we just did a send_message() so it's going to process that in the
// first wait(), the second wait() is then expected to timeout after 2s
//
wait(timeout: 5, mode: wait)

has_message(command: COMMANDS)
if(false: not_unlock)
verify_message(
        command: COMMANDS,
        server: ".",
        service: communicatord,
        required_parameters: {
                list: "DATA,LOCKED,LOCK_FAILED,TRANSMISSION_REPORT,UNLOCKED,UNLOCKING"
        })
clear_message()

wait(timeout: 2, mode: timeout)
has_message()
if(false: wait_timedout)
show_message()
exit(error_message: "wait() for a timeout got a message instead!")

label(name: wait_timedout)

send_message(
	command: UNLOCKING,
	sent_server: my_server,
	sent_service: cluckd,
	server: lock_server,
	service: cluck_test,
	parameters: {
		object_name: "unknown-lock",
		tag: "${tag}"
	})

goto(label: wait_message)

label(name: not_lock)
has_message(command: INVALID)
if(false: not_invalid)
verify_message(
	command: INVALID,
	service: cluckd,
	required_parameters: {
		command: "UNLOCKING",
		message: "the \"object_name\" parameter does not match this cluck object. Got \"unknown-lock\", expected \"unlocking-lock\"."
	})
exit()

label(name: not_invalid)
has_message(command: UNLOCK)
if(false: not_unlock)
verify_message(
	command: UNLOCK,
	service: cluckd,
	required_parameters: {
		object_name: "unlocking-lock",
		tag: "${tag}",
		pid: "${pid}",
		serial: "${serial}"
	},
	// parameters have defaults so they should not be included
	forbidden_parameters: {
		duration,
		timeout,
		type,
		unlock_duration
	})
send_message(
	command: UNLOCKED,
	sent_server: my_server,
	sent_service: cluckd,
	server: lock_server,
	service: cluck_test,
	parameters: {
		object_name: "unlocking-lock",
		tag: "${tag}"
	})
wait(timeout: 1, mode: drain)
exit()

label(name: not_unlock)
exit(error_message: "reached exit too soon -- unexpected message")
